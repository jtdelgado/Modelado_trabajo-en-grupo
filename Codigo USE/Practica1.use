model Practica1

----------Clases-----------
class Avion
attributes
    num_kilometros : Integer --Hace falta hacerla (variable derivada)
            derive : self.vuelos.num_kilometros -> sum()
    num_vuelos : Integer --Hace falta hacerla (variable derivada)
            derive : self.vuelos -> size()
end

associationclass Contrato between
    Avion[0..*] role avion
    Aerolinea[1..*] role aerolinea
attributes
    precio_mes : Real
    num_mes : Integer
    precio_total : Real 
            derive : self.precio_mes * self.num_mes
end

class Aerolinea
attributes
    nombre : String
end

class Aeropuerto
end

class Vuelo
attributes
    identificador : String
    num_kilometros : Integer
end

class Ciudad
attributes 
    nombre : String
end

class Persona
attributes
    nombre : String
    edad : Integer
end

class Piloto < Persona
attributes
    num_kilometros : Integer derive :
        (self.vuelosP.num_kilometros)->union(self.vuelosS.num_kilometros)-> sum()
            -- Se hace una union de los kilometros de vuelosP y vuelosS en un unico conjunto  y se suman los num_kilometros
    piloto_pro : Boolean
       derive : (self.vuelosS ->size() >= 3) and (self.vuelosP -> size() >=2) --------------FALTA POR PONERLO CUANDO SE HAGA LAS ASOCIACIONES
end

class Pasajero < Persona
end

class Almacen
attributes
    capacidad_max : Integer
end

class Hangar < Almacen
end

class Desguace < Almacen
end


---------Fin de Clases--------

----------Asociaciones---------
association vuelosAvion between
    Vuelo [0..*] role vuelos
    Avion [1] role avion
end

association avionVolando between
    Vuelo [0..1] role vueloEnCurso
    Avion [1] role avionEnVuelo
end

association vueloPilotoPrincipal between
    Vuelo [0..*] role vuelosP
    Piloto [1] role pilotoP
end

association vueloPilotoSecundario between
    Vuelo [0..*] role vuelosS
    Piloto [0..1] role pilotoS
end

association vueloPasajero between
    Vuelo [0..1] role vuelos
    Pasajero [0..*] role pasajeros
end

association aeropuertoCiudad between
    Aeropuerto [0..1] role aeropuertos
    Ciudad [1] role ciudad
end

association avionDesguace between
    Avion [0..*] role aviones
    Desguace [0..1] role avionRelegado
end

association avionHangar between
    Avion [0..*] role aviones
    Hangar [0..1] role avionEstacionado
end

----------Fin Asociaciones-----

----------Composiciones----------
composition aeropuertoHangar between
    Aeropuerto [1] role aeropuerto
    Hangar [1..*] role hangares
end

composition aeropuertoDesguace between
    Aeropuerto [1] role aeropuerto
    Desguace [0..1] role desguace
end
----------Fin composiciones----------

----------Invariantes----------
constraints

context Aerolinea
    inv AerolineaNombreUnico:
        Aerolinea.allInstances() -> forAll(a1, a2 | a1 <> a2 implies a1.nombre <> a2.nombre)
                        --a1 <> a2 indica que a1 y a2 son distintos y
                        --a1.nombre <> a2.nombre establece que el nombre de las aerolineas no puede ser igual
                        --La lectura literal seria: si a1 es distinto de a2 entonces a1.nombre es distinto de a2.nombre

context Avion
    inv AvionDesguace:
        self.num_vuelos > 1000 implies not self.avionRelegado.oclIsUndefined()
        -- que el avión tenga más de 1000 vuelos implica que la relación con desguace existe
    inv AvionEstadoUnico:
        (self.vueloEnCurso -> size() + self.avionRelegado -> size() + self.avionEstacionado -> size()) = 1      
        -- obligatoriamente tiene que tener un estado y debe ser solo uno       

context Vuelo
    inv IdVueloUnico:
        Vuelo.allInstances() -> forAll(v1,v2|  v1 <> v2 implies v1.identificador <> v2.identificador)
                        -- Si el objeto no es el mismo esto implica que los identificadores son distintos
    inv PilotosDistintos:
        self.pilotoP <> self.pilotoS
        -- El piloto principal y el secundario no pueden ser el mismo

context Piloto
    inv MaxAerolineasPorPiloto:
        self.vuelosP.avion.aerolinea -> union(self.vuelosS.avion.aerolinea) -> asSet()->size()<3        
    
context Hangar
    inv CapMaxHangar:
        self.aviones -> size() <= self.capacidad_max

context Desguace
    inv CapMaxDesguace:
        self.aviones -> size() <= self.capacidad_max
----------Fin Invariantes------